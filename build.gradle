import org.apache.tools.ant.filters.ReplaceTokens
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'eclipse'
    id 'com.github.johnrengelman.shadow' version '5.0.0'    
}

allprojects {

	version = pluginVersion
	group = pluginGroup	

	sourceCompatibility = 1.11
	targetCompatibility = 1.11

	repositories {
		jcenter()
  	  	maven{ url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
   	 	maven{ url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
    
	processResources {
    	from(sourceSets.main.resources.srcDirs) {
        	filter ReplaceTokens, tokens: [version: version]
			filter ReplaceTokens, tokens: [name: pluginName]
			filter ReplaceTokens, tokens: [mainClassName: mainClassName]
			filter ReplaceTokens, tokens: [groupId: pluginGroup]
			filter ReplaceTokens, tokens: [author: pluginAuthor]
			filter ReplaceTokens, tokens: [apiVersion: pluginApiVersion]
		}
    }
}

dependencies {

	//Spigot API
	compileOnly 'org.spigotmc:spigot-api:1.16.4-R0.1-SNAPSHOT'
	
	//Apache Commons IO
	compileOnly 'commons-io:commons-io:2.6'
	
	//ClassIndex annotation processor
	compile('org.atteo.classindex:classindex:3.4')
	annotationProcessor('org.atteo.classindex:classindex:3.4')
}

task testJar(type: ShadowJar) {
	description 'Build a test Jar'
	archiveClassifier = 'DEV'
	
	from sourceSets.main.output
	configurations = [project.configurations.runtime]
	
	mergeServiceFiles {
    	path = 'META-INF/annotations'
  	}
	
	destinationDirectory = file("$rootDir/server/plugins")
}

task releaseJar(type: ShadowJar) {
	description 'Build a release Jar'
	archiveClassifier = 'RELEASE'
	
	from sourceSets.main.output
	configurations = [project.configurations.runtime]
	
	destinationDirectory = file("$rootDir/releases")
}

//This task is for GitHub Actions only
task ghActions(type: ShadowJar) {	
	description 'GitHub Actions Task. Do not use in regular development!'
	version = ''
	archiveClassifier = ''
	archiveBaseName = 'output'
	
	from sourceSets.main.output
	configurations = [project.configurations.runtime]
	
	destinationDirectory = file("$rootDir/actions")
}